<div className="breadcrumbs">
  <span className="breadcrumb">
    <a href="/reference">Reference</a>
  </span>
  <span> / </span>
  <span className="breadcrumb">
    <a href="/reference/api-reference">API reference</a>
  </span>
</div>

# Schema reference

## `connector`

Defines a [connector](/concepts/components/teo-server/connector)
<span class="concept">CONCEPT</span> in the Teo schema. This config block is
required.

### Fields

A `connector` config block accepts the following fields:

| Name       | Required | Type             | Description                                   |
| :--------- | :------- | :--------------- | :-------------------------------------------- |
| `provider` | **Yes**  | DatabaseProvider | Describes which database connector to use.    |
| `url`      | **Yes**  | String (URL)     | Connection URL including authentication info. |

### Types

A `connector` config block defines the following types:

#### `DatabaseProvider`

`DatabaseProvider` is an enum describes which database connector to use.

```teo showLineNumbers
enum DatabaseProvider {
  mysql
  postgres
  sqlite
  mongo
}
```

### Remarks

* You can only have one `connector` config block in a schema.
* You can give `connector` a name just like any other config blocks - for
example, `connector mysql` or `connector db`.

### Examples

#### Specify a PostgreSQL connector

This example introduces how to connect to a PostgreSQL database. Let's say, the
target database is available with the following credentials:

* User: johndoe
* Password: mypassword
* Host: localhost
* Port: 5432
* Database name: mydb
* Schema name: public

```teo showLineNumbers
connector db {
  provider .postgres
  url      "postgresql://johndoe:mypassword@localhost:5432/mydb?schema=public"
}
```

#### Specify a PostgreSQL connector via an environment variable

In this example, the target database is available with an environment variable:

```teo showLineNumbers
connector db {
  provider .postgres
  url      ENV["DATABASE_URL"]
}
```

When running a Teo CLI command that needs the database connection URL, you need
to make sure that the environment variable is set.

One way to do so is by creating a `.env` file in your project root directory
with the following contents. Teo CLI will automatically pick up your
environment variables defined in this file.

```env title=".env"
DATABASE_URL=postgresql://johndoe:mypassword@localhost:5432/mydb?schema=public
```

#### Specify a MySQL connector

This example introduces how to connect to a MySQL database. In this example,
the target database is available with the following credentials:

* User: johndoe
* Password: mypassword
* Host: localhost
* Port: 3306
* Database name: mydb

```teo showLineNumbers
connector db {
  provider .mysql
  url      "mysql://johndoe:mypassword@localhost:3306/mydb"
}
```

#### Specify a SQLite connector

This example introduces how to connect to a SQLite database. In this example,
the target database is located in a file called dev.db:

```teo showLineNumbers
connector db {
  provider .sqlite
  url      "file:./dev.db"
}
```

#### Specify a MongoDB connector

This example introduces how to connect to a MongoDB database. In this example,
the target database is available with the following credentials:

* User: root
* Password: password
* Host: cluster1.test1.mongodb.net
* Port: N/A
* Database name: testing

```teo showLineNumbers
connector db {
  provider .mongo
  url      "mongodb+srv://root:password@cluster1.test1.mongodb.net/testing?retryWrites=true&w=majority"
}
```

#### Specify a default URL besides environment variable

Teo schema language supports nullish coalescing. A database url can be provided
with a default value when environment variable is not defined.

```teo showLineNumbers
connector db {
  provider .mongo
  url      ENV["MONGO_URL"] ?? "mongodb://127.0.0.1:27017/testing"
}
```

## `server`

Defines a [server](/concepts/components/teo-server/server)
<span class="concept">CONCEPT</span> in the Teo schema. This config block is
required.

### Fields

A `server` config block accepts the following fields:

| Name         | Required | Type          | Description                                           |
| :----------- | :------- | :------------ | :---------------------------------------------------- |
| `bind`       | **Yes**  | (String, Int) | Describes which IP and port this Teo server binds to. |
| `jwtSecret`  | No       | String?       | The JWT token used for user authentication.           |
| `pathPrefix` | No       | String?       | The path prefix to prepend to the request URL.        |

### Remarks

* A `server` config block is required for Teo server to start.
* You can only have one `server` config block in a schema.
* You can give `server` a name just like any other config blocks - for example,
`server http` or `server myServer`.

### Examples

#### Listening on port X

This example introduces how to write `server` config block in order to run a
Teo server. In this example, this server is running on port 5100.

```teo showLineNumbers
server http {
  bind ("0.0.0.0", 5100)
}
```

#### Specify a JWT secret

If your server supports user sessions and permissions, `jwtSecret` should not
be omitted. In this example, its value is set to a long random string.

```teo showLineNumbers
server http {
  bind      ("0.0.0.0", 5100)
  jwtSecret "1234567890_+~qwertyuiopasdfghjklzxcvbnm<>?"
}
```

#### Specify a JWT secret with environment variable

You can use an environment variable to secure your server secrets from being
read by others.

```teo showLineNumbers
server http {
  bind      ("0.0.0.0", 5100)
  jwtSecret ENV["JWT_SECRET"]
}
```

#### Specify a path prefix

In this example, the server serves under `"/v1"` path.

```teo showLineNumbers
server http {
  bind       ("0.0.0.0", 5100)
  pathPrefix "/v1"
}
```

## `entity`

Defines an [entity generator](/concepts/)
<span class="concept">CONCEPT</span> in the Teo schema. This config block is
optional.

### Fields

A `entity` config block accepts the following fields:

| Name       | Required | Type           | Description                                                                      |
| :--------- | :------- | :------------- | :------------------------------------------------------------------------------- |
| `provider` | **Yes**  | EntityProvider | Describes which server programming language to use for generated model entities. |
| `dest`     | **Yes**  | String (Path)  | Into which directory the model entities are generated.                           |

### Types

A `entity` config block defines the following types:

#### `EntityProvider`

`EntityProvider` is an enum describes which server programming language to use
for generated model entities.

```teo showLineNumbers
enum EntityProvider {
  rust
  node
  python
}
```

### Remarks

* You can have zero or more `entity` config blocks in a schema. But normally
only one is needed.
* You need to give `entity` a name if there are more than one entity generators
- for example, `entity rust` or `entity js`.

### Examples

#### Generate Rust model entities inside the project

This example demostrates how to declare an entity generator config block which
generates model entities for the current working Rust project.

```teo showLineNumbers
entity rust {
  provider .rust
  dest "./src/entities"
}
```

#### Generate Node.js model entities inside the project

This example demostrates how to declare an entity generator config block which
generates model entities for the current working javaScript or TypeScript project.

```teo showLineNumbers
entity rust {
  provider .node
  dest "./src/entities"
}
```

#### Generate Python model entities inside the project

This example demostrates how to declare an entity generator config block which
generates model entities for the current working Python project.

```teo showLineNumbers
entity rust {
  provider .python
  dest "./entities"
}
```

## `client`

Defines an [client generator](/concepts/)
<span class="concept">CONCEPT</span> in the Teo schema. This config block is
optional.

### Fields

| Name       | Required | Type           | Description                                            |
| :--------- | :------- | :------------- | :----------------------------------------------------- |
| `provider` | **Yes**  | ClientProvider | Describes what type of client package to generate.     |
| `dest`     | **Yes**  | String (Path)  | Into which directory the client is generated.          |
| `host`     | **Yes**  | String (URL)   | To which host this client connects to.                 |
| `package`  | No       | Bool           | Generate a full package or generate just client files. |

### Types

A `client` config block defines the following types:

#### `ClientProvider`

`ClientProvider` is an enum describes which client language to use for
generated query clients.

```teo showLineNumbers
enum ClientProvider {
  javaScript
  typeScript
  swift
  kotlin
  cSharp
  dart
}
```

### Remarks

* You can have zero or more `client` config blocks in a schema. But normally
only one is needed.
* You need to give `client` a name if there are more than one entity generators
- for example, `client swift` or `client kotlin`.

### Examples

#### Generate javaScript client package

This example demostrates how to declare an client generator config block which
generates client package into a directory.

```teo showLineNumbers
entity rust {
  provider .javaScript
  dest     "../my-client"
  host     "http://localhost:5100"
  package  true
}
```

#### Generate javaScript client code

This example demostrates how to declare an client generator config block which
generates client code without a package into a directory.

```teo showLineNumbers
entity rust {
  provider .javaScript
  dest     "../my-website/src/api-client"
  host     "http://localhost:5100"
  package  false
}
```

#### Generate TypeScript client package

This example demostrates how to declare an client generator config block which
generates client package into a directory.

```teo showLineNumbers
entity rust {
  provider .typeScript
  dest     "../my-client"
  host     "http://localhost:5100"
  package  true
}
```

#### Generate TypeScript client code

This example demostrates how to declare an client generator config block which
generates client code without a package into a directory.

```teo showLineNumbers
entity rust {
  provider .typeScript
  dest     "../my-website/src/api-client"
  host     "http://localhost:5100"
  package  false
}
```

#### Generate Swift client package

This example demostrates how to declare an client generator config block which
generates client package into a directory.

```teo showLineNumbers
entity rust {
  provider .swift
  dest     "../my-client"
  host     "http://localhost:5100"
  package  true
}
```

#### Generate Swift client code

This example demostrates how to declare an client generator config block which
generates client code without a package into a directory.

```teo showLineNumbers
entity rust {
  provider .swift
  dest     "../my-website/src/api-client"
  host     "http://localhost:5100"
  package  false
}
```

#### Generate Kotlin client package

This example demostrates how to declare an client generator config block which
generates client package into a directory.

```teo showLineNumbers
entity rust {
  provider .kotlin
  dest     "../my-client"
  host     "http://localhost:5100"
  package  true
}
```

#### Generate Kotlin client code

This example demostrates how to declare an client generator config block which
generates client code without a package into a directory.

```teo showLineNumbers
entity rust {
  provider .kotlin
  dest     "../my-website/src/api-client"
  host     "http://localhost:5100"
  package  false
}
```

#### Generate C# client package

This example demostrates how to declare an client generator config block which
generates client package into a directory.

```teo showLineNumbers
entity rust {
  provider .cSharp
  dest     "../my-client"
  host     "http://localhost:5100"
  package  true
}
```

#### Generate C# client code

This example demostrates how to declare an client generator config block which
generates client code without a package into a directory.

```teo showLineNumbers
entity rust {
  provider .cSharp
  dest     "../my-website/src/api-client"
  host     "http://localhost:5100"
  package  false
}
```

#### Generate Dart client package

This example demostrates how to declare an client generator config block which
generates client package into a directory.

```teo showLineNumbers
entity rust {
  provider .dart
  dest     "../my-client"
  host     "http://localhost:5100"
  package  true
}
```

#### Generate Dart client code

This example demostrates how to declare an client generator config block which
generates client code without a package into a directory.

```teo showLineNumbers
entity rust {
  provider .dart
  dest     "../my-website/src/api-client"
  host     "http://localhost:5100"
  package  false
}
```

#### Use environment variable to specify client code directory

Different developers generate client into different directories. Use an
environment variable to have each developer generate to their own directory.

```teo showLineNumbers
entity rust {
  provider .dart
  dest     ENV["CLIENT_DIR"]
  host     "http://localhost:5100"
  package  false
}
```

## `model`

Defines a Teo [model](/concepts/components/teo-server/model)
<span class="concept">CONCEPT</span>.

### Remarks

* Every record of a model must be uniquely identifiable. You must define one
field with `@id` decorator or decorate the model with `@id` with multiple
fields to achieve a compound identifier.

#### Model naming convension

* Model names must start with a letter and are typically spelled in PascalCase.
* Model names should use the singular pascal case form (for example, `User`
instead of `user`, `users` or `Users`)

> **Note:** You can use the @map decorator to map a model (for example, User)
> to a table with a different name that does not match model naming conventions
> (for example, users).

### Examples

#### A model named User with two scalar fields

```teo showLineNumbers
model User {
  @id
  email: String
  name: String?
}
```

#### A model named Student with a composite ID

```teo showLineNumbers
@id([.class, .name])
model Student {
  class: String
  name: String
}
```

## Model Decorators

Model decorators modify the behavior of a model.

### `@id`

Defines a multi-field composite ID on the model.

#### Remarks

* This is `PRIMARY KEY` for SQL databases. For MongoDB, a compound unique index
is used and `_id` becomes a hidden field.
* Can be defined on string and number types.

#### Signature

```teo
@id(ScalarFieldEnum<Self>[])
```

#### Arguments

| Position | Required | Type                                      | Description            |
| :------- | :------- | :---------------------------------------- | :--------------------- |
| 0        | **Yes**  | ScalarFieldEnum<>{"<"}</>Self<>{">"}</>[] | A list of field names. |

#### Examples

##### Specify a multi-field ID on two String fields

```teo showLineNumbers
@id([.class, .name])
model Student {
  class: String
  name: String
  email: String?
  age: Int?
}
```

### `@unique`

Defines a multi-field unique composite constraint on the model.

#### Remarks

* This is `UNIQUE` for SQL databases. For MongoDB, a compound unique index
is created.
* Can be defined on string, bool, date and number types.

#### Signature

```teo
@unique(ScalarFieldEnum<Self>[])
```

#### Arguments

| Position | Required | Type                                      | Description            |
| :------- | :------- | :---------------------------------------- | :--------------------- |
| 0        | **Yes**  | ScalarFieldEnum<>{"<"}</>Self<>{">"}</>[] | A list of field names. |

#### Examples

##### Specify a multi-field unique constraint on three fields

```teo showLineNumbers
@unique([.name, .year, .artist])
model Album {
  @id
  id: Int
  name: String
  year: Int
  artist: String
}
```

### `@index`

Defines a multi-field composite index on the model.

#### Remarks

* This is `INDEX` for SQL databases. For MongoDB, a compound index is created.
* Can be defined on string, bool, date and number types.

#### Signature

```teo
@index(ScalarFieldEnum<Self>[])
```

#### Arguments

| Position | Required | Type                                      | Description            |
| :------- | :------- | :---------------------------------------- | :--------------------- |
| 0        | **Yes**  | ScalarFieldEnum<>{"<"}</>Self<>{">"}</>[] | A list of field names. |

#### Examples

##### Specify a multi-field composite index on two fields

```teo showLineNumbers
@index([.year, .name])
model Album {
  @id
  id: Int
  name: String
  year: Int
  artist: String
}
```

### `@map`

Maps the Teo schema model name to a different table name. By default, plural
form is used.

#### Signature

```teo
@map(String)
```

#### Arguments

| Position | Required | Type   | Description                            |
| :------- | :------- | :----- | :------------------------------------- |
| 0        | **Yes**  | String | The database table or collection name. |

#### Examples

##### Map `Person` model to a database table with the name `"people"`

```teo showLineNumbers
@map("people")
model Person {
  @id
  id: Int
  name: String
  age: Int
}
```

### `@virtual`

Defines a [virtual model](/concepts/components/teo-server/model#virtual-model)
<span class="concept">CONCEPT</span>.

#### Remarks

* A virtual model doesn't have underlying database representation. Thus, it
cannot be referenced in a relationship.
* A virtual model do have query client methods. You are responsible for write
actual code to perform `findMany`, `create`, `update` and other action
handlers.

#### Signature

```teo
@virtual
```

#### Examples

##### Specify a virtual model named User

```teo showLineNumbers
@virtual
model User {
  @id
  id: Int
  @unique
  email: String
  @writeonly
  password: String
}
```

### `@identity`

Defines an [identity model](/concepts/components/teo-server/model#identity-model)
<span class="concept">CONCEPT</span>.

#### Remarks

* An identity model has `signIn` and `identity` actions
* Identity model should have at least a field decorated with
[`@identity`](#identity-1) and a field decorated with
[`@identityChecker`](#identitychecker)
* `jwtSecret` in server config block is required to use identity model

#### Signature

```teo
@identity
```

#### Examples

##### Specify an identity model named User

```teo showLineNumbers
@identity
model User {
  @id
  id: Int
  @unique @identity
  email: String
  @writeonly @identityChecker($bcryptVerify($self.get(.password)))
  password: String
}
```

### `@beforeSave`

This pipeline callback is triggered before an object is being saved.

#### Signature

```teo
@beforeSave(Pipeline<Null, Void>)
```

#### Arguments

| Position | Required | Type                                   | Description                   |
| :------- | :------- | :------------------------------------- | :---------------------------- |
| 0        | **Yes**  | Pipeline<>{"<"}</>Null, Void<>{">"}</> | The pipeline being triggered. |

#### Examples

##### Call a custom callback before object is saved

```teo showLineNumbers
@beforeSave($self.print)
model AuthCode {
  @id
  id: Int
  @unique
  email: String
  @internal @onSave($randomDigits(4))
  code: String
}
```

### `@afterSave`

This pipeline callback is triggered after an object is being saved.

#### Signature

```teo
@afterSave(Pipeline<Null, Void>)
```

#### Arguments

| Position | Required | Type                                   | Description                   |
| :------- | :------- | :------------------------------------- | :---------------------------- |
| 0        | **Yes**  | Pipeline<>{"<"}</>Null, Void<>{">"}</> | The pipeline being triggered. |

#### Examples

##### Call a custom callback after object is saved

```teo showLineNumbers
@afterSave($self.print)
model AuthCode {
  @id
  id: Int
  @unique
  email: String
  @internal @onSave($randomDigits(4))
  code: String
}
```

### `@beforeDelete`

This pipeline callback is triggered before an object is being deleted.

#### Signature

```teo
@beforeDelete(Pipeline<Null, Void>)
```

#### Arguments

| Position | Required | Type                                   | Description                   |
| :------- | :------- | :------------------------------------- | :---------------------------- |
| 0        | **Yes**  | Pipeline<>{"<"}</>Null, Void<>{">"}</> | The pipeline being triggered. |

#### Examples

##### Call a custom callback before object is deleted

```teo showLineNumbers
@beforeDelete($self.print)
model AuthCode {
  @id
  id: Int
  @unique
  email: String
  @internal @onSave($randomDigits(4))
  code: String
}
```

### `@afterDelete`

This pipeline callback is triggered after an object is being deleted.

#### Signature

```teo
@afterDelete(Pipeline<Null, Void>)
```

#### Arguments

| Position | Required | Type                                   | Description                   |
| :------- | :------- | :------------------------------------- | :---------------------------- |
| 0        | **Yes**  | Pipeline<>{"<"}</>Null, Void<>{">"}</> | The pipeline being triggered. |

#### Examples

##### Call a custom callback before object is deleted

```teo showLineNumbers
@afterDelete($self.print)
model AuthCode {
  @id
  id: Int
  @unique
  email: String
  @internal @onSave($randomDigits(4))
  code: String
}
```

### `@disable`

Disable actions on the model.

#### Signature

```teo
@disable(ActionOption|ActionOption[])
```

#### Arguments

| Position | Required | Type                             | Description        |
| :------- | :------- | :------------------------------- | :----------------- |
| 0        | **Yes**  | ActionOption&#124;ActionOption[] | Action matcher(s). |

#### Examples

##### Disable entry level create

```teo
@disable(.create | .entry)
model User {
  @id
  id: Int
  name: String
}
```

##### Disable nested level create

```teo
@disable(.create | .nested)
model User {
  @id
  id: Int
  name: String
}
```

##### Disable all but create and find

```teo
@disable(~(.create | .find))
model User {
  @id
  id: Int
  name: String
}
```

### `@action`

Perform action transformers and redirections.

#### Signature

```teo
@action(Pipeline<Teon, Teon>)
```

#### Arguments

| Position | Required | Type                         | Description         |
| :------- | :------- | :--------------------------- | :------------------ |
| 0        | **Yes**  | Pipeline&#60;Teon, Teon&#62; | Action transformer. |

#### Examples

##### Create with a field value altered

```teo
@action($when(.create, $set(["create", "value"], 5)))
model Number {
  @id
  id: Int
  value: Int
}
```

## `model` fields

[Fields](/concepts/components/teo-server/field)
<span class="concept">CONCEPT</span> are content of models.

### Remarks

* A model field contains a field name, a field type, and optionally a list of
field decorators.
* Model fields are divided into three categories:
[fields](/concepts/components/teo-server/field) <span class="concept">CONCEPT</span>,
[relations](/concepts/components/teo-server/relation) <span class="concept">CONCEPT</span>, and
[properties](/concepts/components/teo-server/property) <span class="concept">CONCEPT</span> in nature.

#### Naming convensions

* Must start with a letter
* Typically spelled in camelCase
* letters, digits and underscores are allowed

### Examples

##### A model with fields and relations

```teo showLineNumbers
model Article {
  @id
  id: Int
  name: String
  @foreignKey
  authorId: Int
  @relation(fields: .authorId, references: .id)
  author: Author
}
```

## `model` field scalar types

The database connector determines what native database type each of Teo scalar
type maps to. Similarly, the model entity generator and client entity generator
determine what type in the target programming language each of these types map
to.

### `String`

String represents text.

#### Default type mappings

| Connector  | Default mapping |
| :--------- | :-------------- |
| MySQL      | VARCHAR(191)    |
| PostgreSQL | text            |
| SQLite     | TEXT            |
| MongoDB    | String          |

### `Bool`

Bool represents boolean values aka true and false.

#### Default type mappings

| Connector  | Default mapping |
| :--------- | :-------------- |
| MySQL      | TINYINT(1)      |
| PostgreSQL | boolean         |
| SQLite     | INTEGER         |
| MongoDB    | Bool            |

### `Int`

Int represents integer number values.

#### Remarks

* By default, `Int` is 32 bits. `Int32` and `Int64` is available for optimization.

#### Default type mappings

| Connector  | Default mapping |
| :--------- | :-------------- |
| MySQL      | INT             |
| PostgreSQL | integer         |
| SQLite     | INTEGER         |
| MongoDB    | Int             |

### `Float`

Float represents floating point number values.

#### Remarks

* By default, `Float` is 64 bits. `Float32` and `Float64` is available for
optimization.

#### Default type mappings

| Connector  | Default mapping  |
| :--------- | :--------------- |
| MySQL      | DOUBLE           |
| PostgreSQL | double precision |
| SQLite     | REAL             |
| MongoDB    | Double           |

### `Date`

Date represents date values including `year`, `month` and `day`.

#### Default type mappings

| Connector  | Default mapping |
| :--------- | :-------------- |
| MySQL      | DATE            |
| PostgreSQL | DATE            |
| SQLite     | String          |
| MongoDB    | Date            |

### `DateTime`

DateTime represents date time values.

#### Default type mappings

| Connector  | Default mapping |
| :--------- | :-------------- |
| MySQL      | DATETIME(3)     |
| PostgreSQL | timestamp(3)    |
| SQLite     | String          |
| MongoDB    | Date            |

## Field decorators

Field decorators modify the behavior of a model data field.

### `@id`

Defines an unique ID on the field.

#### Remarks

* This is `PRIMARY KEY` for SQL databases. For MongoDB, a unique index is used
and `_id` becomes a hidden field.
* Can be defined on string and number types.
* Most of the times, you want the database to generate an ID and make it
readonly.

#### Examples

##### Specify an readonly auto generated unique ID

```teo showLineNumbers
model Student {
  @id @autoIncrement @readonly
  id: Int
  class: String
  name: String
  email: String?
  age: Int?
}
```

### `@unique`

Defines a unique constraint on the field.

#### Remarks

* This is `UNIQUE` for SQL databases. For MongoDB, a unique index is created.
* Can be defined on string, bool, date and number types.

#### Examples

##### Specify a unique constraint on a field

```teo showLineNumbers
model User {
  @id @autoIncrement @readonly
  id: Int
  @unique
  name: String
}
```

### `@index`

Defines an index on the field.

#### Remarks

* This is `INDEX` for SQL databases. For MongoDB, an index is created.
* Can be defined on string, bool, date and number types.

#### Examples

##### Specify an index on a field

```teo showLineNumbers
model Album {
  @id
  id: Int
  name: String
  @index
  year: Int
  artist: String
}
```

### `@map`

Maps the field name to a different database table column name. By default, the
original field name is used as is.

#### Signature

```teo
@map(String)
```

#### Arguments

| Position | Required | Type   | Description                     |
| :------- | :------- | :----- | :------------------------------ |
| 0        | **Yes**  | String | The database table column name. |

#### Examples

##### Map `userId` field to a database column with the name `"user_id"`

```teo showLineNumbers
model Person {
  @id
  id: Int
  @map("user_id")
  userId: Int
}
```

### `@auto`

Defines a field which value is auto generated by the underlying database.

> This modifier only exists for MongoDB connector. Use [`@autoIncrement`](#autoincrement) for SQL
> databases.

#### Examples

##### Declare a MongoDB ID field

```teo showLineNumbers
model User {
  @id @auto @map("_id") @readonly
  id: ObjectId
  name: String
}
```

### `@autoIncrement`

Defines a field which value is auto generated with a incremented counter by the
underlying database.

> This modifier only exists for SQL connectors. Use [`@auto`](#auto) if you are
> using MongoDB.

#### Examples

##### Declare an auto increment ID field

```teo showLineNumbers
model User {
  @id @autoIncrement @readonly
  id: Int
  name: String
}
```

### `@default`

Defines a default value for a field.

#### Remarks

* A value of field type is accepted
* A pipeline which returns a value of field type is accepted
* Use [`auto`](#auto) or [`autoIncrement`](#autoincrement) if you want the
database to generate a default value

#### Signature

```teo
@default(Type<Field>|Pipeline<Any, Type<Field>>>)
```

#### Arguments

| Position | Required | Type                                                      | Description                         |
| :------- | :------- | :-------------------------------------------------------- | :---------------------------------- |
| 0        | **Yes**  | Type<>{"<"}</>Field<>{">"}</>                             | A default value.                    |
| 0        | **Yes**  | Pipeline<>{"<"}</>Any, Type<>{"<"}</>Field<>{">"}</><>{">"}</> | A pipeline returns a default value. |

#### Examples

##### Define a field with a default value

```teo showLineNumbers
model Adult {
  @id
  id: Int
  @default(18)
  age: Int
}
```

##### Define a field with a default value from pipeline result

```teo showLineNumbers
model Post {
  @id
  name: String
  @default($self.get(.name))
  localizedName: String
}
```

### `@foreignKey`

Defines a foreign key field.

#### Remarks

* Teo requires this marking
* Teo doesn't create a foreign key on the database level. Instead, Teo tracks
foreign key relationships when delete and update.
* Manually modifying a foreign key may interrupt the relationship between
records.
* A foreign key should be used in a relationship.

#### Examples

##### Declare a foreign key used in a relationship

```teo showLineNumbers
model User {
  @id
  id: Int
  @foreignKey
  profileId: Int
  @relation(fields: .profileId, references: .id)
  profile: Profile
}
```

### `@readonly`

Defines a readonly field.

#### Remarks

* You can use a [`@default`](#default) to apply a default value to a readonly
field.

#### Examples

##### Define a readonly field

```teo showLineNumbers
model User {
  @id @readonly @autoIncrement
  id: Int
}
```

### `@writeonly`

Defines a writeonly field.

#### Examples

##### Define a writeonly password field

```teo showLineNumbers
model User {
  @id @readonly @autoIncrement
  id: Int
  @writeonly
  password: String
}
```

### `@internal`

Defines an internal field aka [`@readonly`](#readonly) and
[`@writeonly`](#writeonly).

#### Remarks

* Use pipelines and callbacks to set a value to be internally used.

#### Examples

##### Define an internal auth code field

```teo showLineNumbers
model AuthCode {
  @id
  phone: String
  @internal @onSave($randomDigits(4))
  code: String
}
```

### `@readwrite`

Defines a readwrite field. This is the default behavior.

#### Remarks

* Readwrite is the default field behavior. Only use this decorator when you
want to stress on its accessibility.

#### Examples

##### Define a field marked with readwrite

```teo showLineNumbers
model AuthCode {
  @id
  phone: String
  @readwrite @onSave($randomDigits(4))
  code: String
}
```

### `@writeNonnull`

Defines a write nonnull field.

#### Examples

##### Define a write nonnull enum field

In this example, a user can be created and updated without a sex. Once a valid
sex value is set, this value can still be changed into male or female, but not
null.

```teo showLineNumbers
enum Sex {
  MALE
  FEMALE
}

model User {
  @id
  phone: String
  @writeNonnull
  sex: Sex?
}
```

### `@writeOnCreate`

Defines a write on create field.

#### Examples

##### Define a write on create enum field

In this example, a user can be created and updated with or without a sex. Once
created, this value cannot be changed anymore. A user must be created with a
target sex. Once created, this value cannot be changed, too.

```teo showLineNumbers
enum Sex {
  MALE
  FEMALE
}

model User {
  @id
  phone: String
  @writeOnCreate
  sex: Sex?
  @writeOnCreate
  targetSex: Sex
}
```

### `@writeOnce`

Defines a write once field.

#### Examples

##### Define a write once enum field

In this example, a user can be created without a sex. Once a valid sex is set,
this value can't be changed anymore.

```teo showLineNumbers
enum Sex {
  MALE
  FEMALE
}

model User {
  @id
  phone: String
  @writeOnce
  sex: Sex?
}
```

### `@readIf`

Defines a field which is readable in some cases.

#### Signature

```teo
@readIf(Pipeline<Type<Field>, Any>)
```

#### Arguments

| Position | Required | Type                                                           | Description                |
| :------- | :------- | :------------------------------------------------------------- | :------------------------- |
| 0        | **Yes**  | Pipeline<>{"<"}</>Type<>{"<"}</>Field<>{">"}</>, Any<>{">"}</> | A pipeline that validates. |

#### Examples

##### Define a field which is readable when an other field value is true

```teo showLineNumbers
model Article {
  @id
  id: Int
  readable: Bool
  @readIf($self.get(.readable).isTrue)
  content: String
}
```

### `@writeIf`

Defines a field which is writable in some cases.

#### Signature

```teo
@writeIf(Pipeline<Type<Field>, Any>)
```

#### Arguments

| Position | Required | Type                                                           | Description                |
| :------- | :------- | :------------------------------------------------------------- | :------------------------- |
| 0        | **Yes**  | Pipeline<>{"<"}</>Type<>{"<"}</>Field<>{">"}</>, Any<>{">"}</> | A pipeline that validates. |

#### Examples

##### Define a field which is writable when an other field value is true

```teo showLineNumbers
model Article {
  @id
  id: Int
  writable: Bool
  @writeIf($self.get(.writable).isTrue)
  content: String
}
```

### `@virtual`

Defines a virtual field.

#### Examples

##### Define a virtual field

```teo showLineNumbers
model User {
  @id
  id: Int
  @virtual
  oldPassword: String?
}
```

### `@atomic`

Defines an atomic field.

#### Remarks

* An atomic field can be updated with atomic updator
* `@atomic` can only be applied on number and array fields
* An atomic field cannot have [`@onSet`](#onset) and [`@onSave`](#onsave)
pipeline callbacks.

#### Examples

##### Define an atomic field

```teo showLineNumbers
model City {
  @id
  id: Int
  name: String
  @atomic
  viewCount: Int
}
```

### `@nonatomic`

Defines a nonatomic field.

#### Remarks
* This is the default field behavior.
* Only use this decorator when you want to stress on its atomicity.

#### Examples

##### Define an nonatomic field

```teo showLineNumbers
model City {
  @id
  id: Int
  name: String
  @nonatomic
  viewCount: Int
}
```

### `@presentWith`

Defines a field which is required when some other field value is present.

#### Signature

```teo
@presentWith(ScalarFieldEnum<Self>|ScalarFieldEnum<Self>[]...)
```

#### Arguments

| Position | Required | Type                                      | Description       |
| :------- | :------- | :---------------------------------------- | :---------------- |
| (any)    | **Yes**  | ScalarFieldEnum<>{"<"}</>Self<>{">"}</>   | A field name.     |
| (any)    | **Yes**  | ScalarFieldEnum<>{"<"}</>Self<>{">"}</>[] | Some field names. |

#### Examples

##### Define a calling code which is required when phone number is present

```teo showLineNumbers
model User {
  @id
  id: Int
  @presentWith(.phoneNumber)
  callingCode: String?
  phoneNumber: String?
}
```

### `@presentWithout`

Defines a field which is required when some other field value is missing.

#### Signature

```teo
@presentWithout(ScalarFieldEnum<Self>|ScalarFieldEnum<Self>[]...)
```

#### Arguments

| Position | Required | Type                                      | Description       |
| :------- | :------- | :---------------------------------------- | :---------------- |
| (any)    | **Yes**  | ScalarFieldEnum<>{"<"}</>Self<>{">"}</>   | A field name.     |
| (any)    | **Yes**  | ScalarFieldEnum<>{"<"}</>Self<>{">"}</>[] | Some field names. |

#### Examples

##### Define a phone which is required if email is missing, vice versa

```teo showLineNumbers
model User {
  @id
  id: Int
  @presentWithout(.email)
  phone: String?
  @presentWithout(.phone)
  email: String?
}
```

### `@presentIf`

Defines a field which is required in some cases.

#### Signature

```teo
@presentIf(Pipeline<Type<Field>, Any>)
```

#### Arguments

| Position | Required | Type                                                           | Description                |
| :------- | :------- | :------------------------------------------------------------- | :------------------------- |
| 0        | **Yes**  | Pipeline<>{"<"}</>Type<>{"<"}</>Field<>{">"}</>, Any<>{">"}</> | A pipeline that validates. |

#### Examples

##### Define a field which is required when an other field value is true

```teo showLineNumbers
model Article {
  @id
  id: Int
  contentRequired: Bool
  @presentIf($self.get(.contentRequired).isTrue)
  content: String?
}
```

### `@identity`

Defines a field which is used as an identity for authentication.

#### Remarks

* This decorator is used together with [`@identityChecker`](#identitychecker)
* This decorator requires the field's containing model to have
[`@identity`](#identity) decorated

#### Examples

##### Declare an email field for authentication

```teo showLineNumbers
@identity
model User {
  @id
  id: Int
  @unique @identity
  email: String
  @identityChecker
  password: String
}
```

### `@identityChecker`

Defines a field which is used for validating the authentication.

#### Signature

```teo
@identityChecker(Pipeline<Type<Field>, Any>)
```

#### Arguments

| Position | Required | Type                                                           | Description                                                   |
| :------- | :------- | :------------------------------------------------------------- | :------------------------------------------------------------ |
| 0        | **Yes**  | Pipeline<>{"<"}</>Type<>{"<"}</>Field<>{">"}</>, Any<>{">"}</> | A pipeline that accepts a value of this field type validates. |

#### Remarks

* This decorator is used together with [`@identity`](#identity-1)
* This decorator requires the field's corresponding model to have
[`@identity`](#identity) decorated

#### Examples

##### Declare a password field for authentication

```teo showLineNumbers
@identity
model User {
  @id
  id: Int
  @unique @identity
  email: String
  @identityChecker
  password: String
}
```

### `@onSet`

Define an on set pipeline callback for the field.

#### Signature

```teo
@onSet(Pipeline<Type<Field>, Any>)
```

#### Arguments

| Position | Required | Type                                                           | Description                                                 |
| :------- | :------- | :------------------------------------------------------------- | :---------------------------------------------------------- |
| 0        | **Yes**  | Pipeline<>{"<"}</>Type<>{"<"}</>Field<>{">"}</>, Any<>{">"}</> | A pipeline that perform callbacks with value of this field. |


#### Examples

##### Validate email on value set

```teo showLineNumbers
model User {
  @id
  id: Int
  @unique @onSet($isEmail)
  email: String
}
```

### `@onSave`

Define an on save pipeline callback for the field.

#### Signature

```teo
@onSave(Pipeline<Type<Field>, Any>)
```

#### Arguments

| Position | Required | Type                                                           | Description                                                 |
| :------- | :------- | :------------------------------------------------------------- | :---------------------------------------------------------- |
| 0        | **Yes**  | Pipeline<>{"<"}</>Type<>{"<"}</>Field<>{">"}</>, Any<>{">"}</> | A pipeline that perform callbacks with value of this field. |

#### Examples

##### Generate a default value on save

```teo showLineNumbers
model User {
  @id
  id: Int
  @unique
  email: String
  @onSave($now)
  updatedAt: DateTime
}
```

### `@onOutput`

Define an on output pipeline callback for the field.

#### Signature

```teo
@onOutput(Pipeline<Type<Field>, Any>)
```

#### Arguments

| Position | Required | Type                                                           | Description                                                 |
| :------- | :------- | :------------------------------------------------------------- | :---------------------------------------------------------- |
| 0        | **Yes**  | Pipeline<>{"<"}</>Type<>{"<"}</>Field<>{">"}</>, Any<>{">"}</> | A pipeline that perform callbacks with value of this field. |

#### Examples

##### Adding prefix for a value on output

```teo showLineNumbers
model User {
  @id
  id: Int
  @unique @onOutput($prepend("Email: "))
  email: String
}
```

### `@queryable`

Define a queryable field.

#### Remarks

* This is the default field behavior. Only use this when you want to stress on
the query ability of the field.

#### Examples

##### Define a queryable field

```teo showLineNumbers
model User {
  @id
  id: Int
  @queryable
  age: Int
}
```

### `@unqueryable`

Define an unqueryable field.

#### Examples

##### Define an unqueryable field

```teo showLineNumbers
model User {
  @id
  id: Int
  @unqueryable
  age: Int
}
```

### `@sortable`

Define a sortable field.

#### Remarks

* This is the default field behavior. Only use this when you want to stress on
the sort ability of the field.

#### Examples

##### Define a sortable field

```teo showLineNumbers
model User {
  @id
  id: Int
  @sortable
  age: Int
}
```

### `@unsortable`

Define an unsortable field.

#### Examples

##### Define a unsortable field

```teo showLineNumbers
model User {
  @id
  id: Int
  @unsortable
  age: Int
}
```

## Relation decorators

Relation decorators modify the behavior of a model relation field.

### `@relation`

Define a relation.

#### Remarks

* Unlike Prisma, a relation in Teo must be explicitly declared.
* When specifying `fields` and `references`, an enum value can be specified
instead of an array for simplicity.

#### Signature

```teo
@relation(fields: ScalarFieldEnum<Self>[]?, references: ScalarFieldEnum<Other>[]?>, through: Model?, local: RelationEnum<Through>?, foreign: RelationEnum<Through>?)
```

#### Arguments

| Position   | Required | Type                                     | Description                            |
| :--------- | :------- | :--------------------------------------- | :------------------------------------- |
| fields     | No       | ScalarFieldEnum<>{"<"}</>Self<>{">"}</>  | Local fields.                          |
| references | No       | ScalarFieldEnum<>{"<"}</>Other<>{">"}</> | Foreign fields.                        |
| through    | No       | Model                                    | Model for join table.                  |
| local      | No       | RelationEnum<>{"<"}</>Through<>{">"}</>  | Relation to this model on join table.  |
| foreign    | No       | RelationEnum<>{"<"}</>Through<>{">"}</>  | Relation to other model on join table. |

#### Examples

##### Declare a one to one relation

```teo showLineNumbers
model User {
  @id
  id: Int
  name: String
  @relation(fields: .id, references: .userId)
  profile: Profile
}

model Profile {
  @id
  id: Int
  age: Int
  @foreignKey
  userId: Int
  @relation(fields: .userId, references: .id)
  user: User
}
```

##### Declare a one to many relation

```teo showLineNumbers
model User {
  @id
  id: Int
  age: Int?
  name: String
  @relation(fields: .id, references: .userId)
  posts: Post[]
}

model Post {
  @id
  id: Int
  title: String
  @foreignKey
  userId: Int?
  @relation(fields: .userId, references: .id)
  user: User?
}
```

##### Declare a many to many relation

```teo showLineNumbers
model Artist {
  @id
  id: Int
  name: String
  @relation(through: Perform, local: .artist, foreign: .song)
  songs: Song[]
}

model Song {
  @id
  id: Int
  name: String
  @relation(through: Perform, local: .song, foreign: .artist)
  artists: Artist[]
}

@id([.artistId, .songId])
model Perform {
  @foreignKey
  artistId: Int
  @foreignKey
  songId: Int
  @relation(fields: .artistId, references: .id)
  artist: Artist
  @relation(fields: .songId, references: .id)
  song: Song
}
```

## Property decorators

Relation decorators modify the behavior of a model property field.

### `@getter`

Define a property getter.

#### Signature

```teo
@getter(Pipeline<Null, Type<Field>>)
```

#### Arguments

| Position | Required | Type                                        | Description                         |
| :------- | :------- | :------------------------------------------ | :---------------------------------- |
| 0        | **Yes**  | Pipeline&#60;Null, Type&#60;Field&#62;&#62; | A pipeline to get property's value. |

#### Examples

##### Define a readonly property

```teo showLineNumbers
model User {
  @id
  id: Int
  firstName: String
  lastName: String
  @getter($self.get(.firstName).append(" ").append($self.get(.lastName)))
  name: String
}
```

### `@setter`

Define a property setter.

#### Signature

```teo
@setter(Pipeline<Null, Any>)
```

#### Arguments

| Position | Required | Type                        | Description                                |
| :------- | :------- | :-------------------------- | :----------------------------------------- |
| 0        | **Yes**  | Pipeline&#60;Null, Any&#62; | A pipeline to assign values to the object. |

#### Examples

##### Define a shorthand writeonly property

```teo
model User {
  @id
  id: Int
  firstName: String
  lastName: String
  @setter($split(" ").assign(.firstName, $at(0)).assign(.lastName, $at(1)))
  name: String
}
```

### `@cached`

Define a cached property.

#### Remarks

* A cached property has a database column.
* Unlike a field, cached property's value is calculated.
* [`@dependencies`](#dependencies) must be specified

#### Examples

##### Define a cached property

```teo showLineNumbers
model User {
  @id
  id: Int
  firstName: String
  lastName: String
  @getter($self.get(.firstName).append(" ").append($self.get(.lastName)))
  @cached @dependencies([.firstName, .lastName])
  name: String
}
```

### `@dependencies`

Define field dependencies for a property.

#### Signature

```teo
@dependencies(ScalarFieldEnum<Self>[])
```

#### Arguments

| Position | Required | Type                            | Description        |
| :------- | :------- | :------------------------------ | :----------------- |
| 0        | **Yes**  | ScalarFieldEnum&#60;Self&#62;[] | Dependency fields. |


#### Examples

##### Define dependencies for a property

```teo showLineNumbers
model User {
  @id
  id: Int
  firstName: String
  lastName: String
  @getter($self.get(.firstName).append(" ").append($self.get(.lastName)))
  @dependencies([.firstName, .lastName])
  name: String
}
```

## `enum`

Defines an [enum](/concepts/components/teo-server/enum)
<span class="concept">CONCEPT</span>.

### Remarks

* Enums are natively supported by PostgreSQL and MySQL
* Enums are implemented and enforced at Teo level in other databases

#### Naming conventions

* Enum names must start with a letter
* Typically spelled in PascalCase
* letters, digits and underscores are allowed

### Examples

#### Specify an `enum` with two possible values

```teo
enum Role {
  USER
  ADMIN
}

model User {
  @id @autoIncrement
  id: Int
  role: Role
}
```

#### Specify an `enum` with two possible values and set a default value

```teo
enum Role {
  USER
  ADMIN
}

model User {
  @id @autoIncrement
  id: Int
  @default(.USER)
  role: Role
}
```
{/*
## Pipeline */}
